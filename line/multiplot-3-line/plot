set term post enh eps color "Helvetica-Bold" 29
set output "./dynamic-cost-saving.eps"
set datafile separator ","
set style fill pattern 2
set bar 5
set grid ytics
set style line 1 lc rgb '#3498db' lt 1 lw 8 pt 2 pi -1 ps 2.5 dashtype 3
set style line 2 lc rgb '#1abc9c' lt 2 lw 8 pt 4 pi -1 ps 2.5 dashtype 2
set style line 3 lc rgb '#e74c3c' lt 3 lw 8 pt 3 pi -1 ps 2.5 dashtype 1

set multiplot

bm = 0.21
lm = 0.15
rm = 0.90
gap = 0.05
size = 0.7

kk = 0.5

set border 1+2+8
set lmargin at screen lm
set rmargin at screen rm
set bmargin at screen bm
set tmargin at screen bm + size * kk

set auto x
set yrange [0 : 50] noreverse nowriteback
set ytics 0, 25, 50
set xrange [0 : 7]
set xtics border in scale 1,0.5 nomirror rotate by -45 offset character 0, 0, 0 
set ylabel "Percentage (%)" font "Helvetica-Bold,30" offset 1,3.5
set xlabel "Number of Bids" font "Helvetica-Bold,30"

set xtics ("1000" 0, "2000" 1, "3000" 2, "4000" 3, "5000" 4, "6000" 5, "7000" 6, "8000" 7)


xpos = 0.2
ypos = 0.3

#axes broken line
set arrow 1  from -0.1,48 to 0.1,52 nohead
set arrow 2  from -0.1,55 to 0.1,59 nohead
set arrow 3  from 6.9,48 to 7.1,52 nohead
set arrow 4  from 6.9,55 to 7.1,59 nohead

#set arrow 2  from screen 0.52,0.08 to screen 0.54,0.12 nohead
#set arrow 3  from screen 0.5,0.88 to screen 0.52,0.92 nohead
#set arrow 4  from screen 0.52,0.88 to screen 0.54,0.92 nohead

set style fill pattern 4 noborder

plot "data.csv" u 0:1 with lp title "" ls 2

set yrange [80 : 100]
set ytics 80, 10, 100
unset xtics
unset xlabel
unset ylabel
set border 2+4+8
set bmargin at screen bm + size * kk + gap
set tmargin at screen bm + size + gap

set key maxrows 1 maxcols 1 font ", 20" at 7, 62

unset arrow 1
unset arrow 2
unset arrow 3
unset arrow 4

plot "data.csv" u 0:4 with lp title "Reserved" ls 2,\
     "" u 0:2 with lp title "Spot" ls 1,\
     "" u 0:3 with lp title "Sublet" ls 3
     
    



