set term post enh eps color "Helvetica-Bold" 30
set output "./mm-trans-energy.eps"
set datafile separator ","
set style fill pattern 2
set grid ytics
set multiplot

set style line 1 lc rgb '#e74c3c' lt 1 lw 10 ps 0.3 pt 3
set style line 2 lc rgb '#3498db' lt 1 lw 10 ps 0.3 pt 2
set style line 3 lc rgb '#34495e' lt 1 lw 8
set style line 4 lc rgb '#D2D7D3' lt 1 lw 8

bm = 0.21
lm = 0.15
rm = 0.90
gap = 0.06
size = 0.7

kk = 0.5

set border 1+2+8
set lmargin at screen lm
set rmargin at screen rm
set bmargin at screen bm
set tmargin at screen bm + size * kk

set auto x
set yrange [0: 3.60] noreverse nowriteback
set ytics 0, 1.2, 3.60 font ",25"
set xrange [0 : 300]
set xtics border in scale 1,0.5 nomirror rotate by -45 offset character 0, 0, 0 
set ylabel "Frequency (GHz)" font ",25" offset 2.5, -0.2
set xlabel "Time (second)" offset 0, 0.4

xpos = 0.2
ypos = 0.3

set arrow from 0, 1.6 to 300,1.6 nohead linestyle 3 dashtype 5
set label "" at 305, 1.6 font ",20"
plot "data.csv" u 0:1 with p title "" ls 2

unset xtics
unset xlabel
unset ylabel
unset label
unset arrow
set yrange [0 : 30]
set ytics 0, 10, 30 font ",25"
set ylabel "Power (W)" font ",25" offset 1.7, -0.2
set border 2+4+8
set bmargin at screen bm + size * kk + gap
set tmargin at screen bm + size + gap

set arrow from 0, 6.22 to 300,6.22 nohead linestyle 3 dashtype 3
set label "" at 305, 6.22  font ",20"
plot "data.csv" u 0:2 with p title "" ls 1



